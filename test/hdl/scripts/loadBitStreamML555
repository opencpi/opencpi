#!/bin/csh -f
if ($#argv != 3) then
    echo $0{}: Usage is: loadBitStream bit_file PCI_device USB_portname
    echo Use "lspci -D" command to find PCI devices, like \"0000:03:00.0\"
    echo Use ocfrp_check with no arguments to find all OCFRP boards.
    echo No good way yet to find out usb ports associated with JTAG boards.
    exit 1
endif
set bin = ./linux-x86_64-bin
set bitfile = $argv[1]
set device = $argv[2]
set usbport = $argv[3]
set temp = /tmp/cpibitstream$$
set tscript =  $temp.1
set tbar = $temp.2
set tlog = $temp.3
set golden = /opt/cpi/ocfrp/pci-config-snapshots/$device
echo Loading bit file $bitfile onto device $device via usb port $usbport.
echo Checking existing bitstream..
sudo $bin/ocfrp_check $device
if ($status != 0) then
   echo Existing bitstream does not appear to be functional.
   if (! -f $golden) then
     echo There is no config snapshot in $golden to restore.
     exit 1
   endif
   echo We will use the golden bitstream in $golden
   set pciConfig = $golden
else
   echo Existing bitstream is ok, proceeding to snapshot the PCI config space
   sudo cp /sys/bus/pci/devices/$device/config $tbar	
   sudo chmod a+rw $tbar
   if ($status != 0) then
      echo Error: we could not snapshot the PCI configuration before loading
      rm $tbar
      exit 1
   endif
   set golden = $tbar
endif
cat > $tscript <<EOF
setMode -bs
setCable -port $usbport
Identify
assignFile -p 1 -file $bitfile
Program -p 1
exit
EOF
set bad = 1
echo Loading bitstream...
csh -c "source /opt/Xilinx/12.3/ISE_DS/settings64.csh; rehash;impact -batch $tscript">&$tlog
if ($status != 0) then
  echo Error: impact bitstream loader failed. See $tlog
  echo Error log says: `grep -i error $tlog`
else
  grep -q 'Programmed successfully' $tlog
  if ($status != 0) then
    echo Error: bit stream loading did not succeed. See $tlog
    echo Error log says: `grep -i error $tlog`
    echo Error: May need to reset with post-reset PCI config snapshot
  else
    echo Bitstream loaded successfully, proceeding to restore PCI config \( from $golden \)
    sleep 2
    sudo cp $golden /sys/bus/pci/devices/$device/config
    if ($status != 0) then
       echo Error: Restoration of PCI config failed.
       echo Error: May need to reset with post-reset PCI config snapshot
    else
       echo PCI Configuration restored, checking that new bitstream is ok.
       sudo $bin/ocfrp_check $device
       if ($status == 0) then
          echo New bitstream is ok.
       else
          set bad = 1
          echo Error: New bitstream is not responding.
       endif
    endif
    set bad = 0
  endif
endif
echo rm $temp.*
exit $bad


